Simple Object
LayLa.Insert
(NEW Button
	(LAYOUT (SET w 70[]))	{ default height, width=70 pixel, extend if possible }
	(ATTR (SET Caption "Open Guide") (SET Cmd "Desktops.OpenDoc LayLa.Guide.Text"))
)


PLACE
LayLa.Insert
(PLACE Panel
	(border=5)
	(NEW List (x=0 y=15 w=50 h=50))
	(NEW Button (x=50 y=50 w=50 h=20)
		(ATTRIBUTES Caption = "Blah"))
)


HLIST
LayLa.Insert
(HLIST Panel (border=8)
	(NEW Button (w=50 h=20)
		(ATTR Caption="Open" Cmd="Desktops.OpenDoc &File.Value"))
	(NEW TextField
		(ATTR Name="File"))
)


extension factor
LayLa.Insert
(TABLE Panel (border=8 cols=3 rows=3 orientation=HOR w=200 h=150)
	(ATTR Locked=TRUE)
	(SPAN 2 2 (NEW Button (w=50[] h=110[3])))
	(NEW Button (w=[] h=[]))
	(NEW Button (w=[] h=[] hjustifyMe=CENTER vjustifyMe=CENTER))
	(NEW Button (w=[2] h=[]))
	(NEW Button (w=[] h=[] hjustifyMe=CENTER vjustifyMe=CENTER))
	(NEW Button (w=[] h=[])))


TABLE with SPAN and BREAK
LayLa.Insert
(TABLE Panel (border=8 cols=3 rows=3)
	(ATTR Locked=TRUE)
	(BREAK (HLIST Panel (border=8) (NEW Caption (ATTR Value="Zwischentitel 0"))))
	(BREAK (HLIST Panel (border=8) (NEW Caption (ATTR Value="Zwischentitel 1"))))
	(NEW Button (ATTR Caption = "1"))
	(SPAN 2 1 (NEW Button (w=[1] h=[1]) (ATTR Caption = "2")))
	(NEW Button  (ATTR Caption = "3"))
	(NEW Button  (ATTR Caption = "4"))
	(NEW Button  (ATTR Caption = "5"))
	(NEW Button  (ATTR Caption = "6"))
	(NEW Button  (ATTR Caption = "7"))
	(BREAK (HLIST Panel (h=60 w=[1] border=8) (HLIST VIRTUAL (vjustifyMe=TOP)
		(NEW Caption (vjustifyMe=CENTER) (ATTR Value="Zwischentitel 2"))
		(NEW Button (hjustifyMe=RIGHT) (ATTR Caption="hello")))
	))	
	(NEW Button  (ATTR Caption = "8"))	
	(NEW Button  (ATTR Caption = "9"))
	(BREAK (HLIST Panel (border=8) (NEW Caption (ATTR Value="Zwischentitel 3"))))	
)))


CONFIG, INCLUDE, same Model in 4 Gadgets
LayLa.Insert
(CONFIG
	(DEF int1 (NEW Integer (ATTR Value=75)))
	(VLIST Panel (border=8)
		(VLIST VIRTUAL (border=8)
			(NEW Slider (w = 100 h = 20) (LINKS Model=int1))
			(NEW TextField (w = 100 h = 20) (LINKS Model=int1)))
		(INCLUDE LayLa.Include.Config (PARAMS (SET int int1) (SET MyBorder 8)))
		(INCLUDE LayLa.Include.Config (MyBorder=3))
		(INCLUDE LayLa.Include.Config (MyBorder=8))
	)
)


SCOPY, DCOPY
LayLa.Insert
(HLIST Panel (border=8)
	(VLIST VIRTUAL (border=8)
		(SCOPY LayLaTest.Slider (w=75 h=15))
		(SCOPY LayLaTest.TextField)
		(SCOPY LayLaTest.Slider)
		(SCOPY LayLaTest.Panel)
	)
	(VLIST VIRTUAL (border=8)
		(DCOPY LayLaTest.Panel)
		(DCOPY LayLaTest.Slider)
		(DCOPY LayLaTest.TextField)
		(DCOPY LayLaTest.Slider (h = 30) (ATTR Value=50))
	)
)


sameSize, grid, justify
LayLa.Insert
(CONFIG
	(DEF Grid (CONFIG
		(DEF vgrid STATIC) (DEF hgrid STATIC) (DEF sameWidth FALSE) (DEF sameHeight FALSE)
		(TABLE Panel (rows=3 cols=3 w=181 h=181 border=8 orientation=HOR hgrid = hgrid vgrid=vgrid
								sameWidth=sameWidth sameHeight=sameHeight hjustify=RIGHT vjustify=TOP)
			(NEW Button (w=[1] h=[1]) (ATTR Caption = "1"))
			(NEW Button (w=20 h=20) (ATTR Caption = "2"))
			(NEW Button (w=[1] h=[1]) (ATTR Caption = "3"))
			(NEW Button (w=20 h=20 hjustifyMe=LEFT) (ATTR Caption = "4"))
			(NEW Button (w=30 h=30 hjustifyMe=CENTER vjustifyMe=CENTER) (ATTR Caption = "5"))
			(NEW Button (w=20 h=20 vjustifyMe=BOTTOM) (ATTR Caption = "6"))
			(NEW Button (w=[1] h=[1]) (ATTR Caption = "7"))
			(NEW Button (w=20 h=20 hjustifyMe=LEFT vjustifyMe=BOTTOM) (ATTR Caption = "8"))
			(NEW Button (w=[1] h=[1]) (ATTR Caption = "9"))
		)))
	(TABLE Panel (cols=3 border=8)
		VIRTUAL
		(NEW Grid)
		VIRTUAL
		(NEW Grid ( hgrid=DYNAMIC))
		(NEW Grid ( vgrid=DYNAMIC))
		(NEW Grid (PARAMS hgrid=DYNAMIC vgrid=DYNAMIC))
		(NEW Grid ( hgrid=DYNAMIC vgrid=DYNAMIC sameWidth=TRUE))
		(NEW Grid (PARAMS hgrid=DYNAMIC vgrid=DYNAMIC sameHeight=TRUE))
		(NEW Grid (PARAMS hgrid=DYNAMIC vgrid=DYNAMIC sameWidth=TRUE sameHeight=TRUE))
))


complex positioning
LayLa.Insert
(TABLE Panel (border=8 cols=3 rows=3 orientation=VERT w=151 h=151)
	(NEW Button (w=[1] h=[1]) (ATTR Caption = "1"))
	(HLIST VIRTUAL (w=[1] h=[1]) (NEW Button (w=20 h=20 hjustifyMe=RIGHT) (ATTR Caption = "2")))
	(NEW Button (w=[1] h=[1]) (ATTR Caption = "3"))
	(HLIST VIRTUAL (w=[1] h=[1]) (NEW Button (w=20 h=20) (ATTR Caption = "4")))
	(NEW Button (w=30 h=30 hjustifyMe = RIGHT vjustifyMe=TOP) (ATTR Caption = "5"))
	(HLIST VIRTUAL (w=[1] h=[1]) (NEW Button (w=20 h=20) (ATTR Caption = "6")))
	(NEW Button (w=[1] h=[1]) (ATTR Caption = "7"))
	(HLIST VIRTUAL (w=[] h=[]) (NEW Button (w=20 h=20 vjustifyMe=TOP) (ATTR Caption = "8")))
	(NEW Button (w=[] h=[] hjustifyMe=RIGHT vjustifyMe=TOP) (ATTR Caption = "9")))


Desktop.Panel
LayLa.Insert
(HLIST Panel (sameSize=TRUE dist=10 border=8)
	(NEW List
		(ATTR (SET Name "List") (SET Cmd "Desktops.OpenDoc '#Point '")))

	(VLIST VIRTUAL (dist=2)
		(NEW List (w=[1] h=100)
			(ATTR (SET Name "List1") (SET Cmd "Lists.Directory '#Point ' List")))

		(TABLE VIRTUAL (sameSize=TRUE dist=2 cols=3)
			(NEW Button (h=20)
				(ATTR Caption="Show" Cmd="Lists.Directory '&Show.Value ' List"))
			(SPAN 1 2 (NEW TextField (h=20)
				(ATTR Name="Show" Cmd="Lists.Directory '#Value ' List")))

			(BREAK (NEW VIRTUAL (h=10)))

			(NEW Button (h=20)
				(ATTR Caption="Open" Cmd="Desktops.OpenDoc &Open.Value "))
			(SPAN 1 2 (NEW TextField (h=20)
				(ATTR Name="Open" Cmd="Desktops.OpenDoc #Value ")))	

			(NEW Button (h=20)
				(ATTR (SET Caption "Open ^") (SET Cmd "Desktops.OpenDoc ^")))
			(SPAN 2 1 (NEW Button
				(ATTR (SET Caption "Panel") (SET Cmd "Desktops.OpenDoc (PanelDoc)"))))
			(NEW Button (h=20)
				(ATTR (SET Caption "Text") (SET Cmd "Desktops.OpenDoc (TextDoc)")))

			(NEW Button (h=20)
				(ATTR (SET Caption "Recall") (SET Cmd "Desktops.Recall")))
			(NEW Button (h=20)
				(ATTR (SET Caption "Icon *") (SET Cmd "Icons.InsertIcon *")))
				
			(NEW Button (h=20)
				(ATTR Caption="Delete" Cmd="System.DeleteFiles &Delete.Value  ~"))
			(SPAN 1 2 (NEW TextField (h=20)
				(ATTR Name="Delete")))
	))
)
